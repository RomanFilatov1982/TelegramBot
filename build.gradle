plugins {
    id 'java-library'
    id 'io.qameta.allure' version '2.11.2' //Плагин io.qameta.allure нужен для генерации отчётов о тестировании в формате Allure Report, совместимых с JUnit/TestNG/Selenide и т. д.
}

repositories {
    mavenCentral()
}

def allureVersion = "2.21.0",
    selenideVersion = "6.19.1"

allure {
    report { //отвечает за появлении папочки build/allure-results
        version.set(allureVersion)
    }
    adapter {
        aspectjWeaver.set(true)
        frameworks {
            junit5 { //название фреймворка
                adapterVersion.set(allureVersion) //версия интеграции фреймворка и аллюр
            }
        }
    }
}

dependencies {
    testImplementation(
            "com.codeborne:selenide:$selenideVersion",
            "io.qameta.allure:allure-selenide:$allureVersion",
            "org.junit.jupiter:junit-jupiter:5.10.0",
            'org.slf4j:slf4j-simple:2.0.7'
    )
}

tasks.withType(Test) {
    useJUnitPlatform()
    systemProperties(System.getProperties())

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}


task web_test(type: Test) {
    useJUnitPlatform {
        includeTags("web")
    }
}